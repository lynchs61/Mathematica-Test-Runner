#!/usr/bin/env wolframscript

(* Created by Mathematica Plugin for IntelliJ IDEA *)

(* :Title: mathematica-test-runner *)
(* :Author: Sean Lynch *)
(* :Date: 2018-01-19 *)

version = "1.0.7";

versionInfo[] :=
    StringTemplate["Version: ``"][version];

usage[] := StringTemplate["
  Usage: `name` [options] [targetFile|targetDirectory]

  Options:

    -V, --version              Output the version number
    -h, --help                 Output usage information
    -R, --reporter <reporter>  Specify a reporter to use. The default is 'spec'.
                               Choose from one of the following options:
                               `reporters`.

  Reporters:

    'spec'        - The default reporter - simply outputs a nested view of the
                    high-level results, showing successes and fails and a short
                    summary at the end.
    'mathematica' - Output the results exactly as they come from the Mathematica
                    front-end.
    'json'        - The results are given as a single JSON object that
                    corresponds to each TestReportObject.
    'tap'         - The TAP reporter emits lines for a Test-Anything-Protocol
                    consumer.
    'junit'       - Results are given in the JUnit format
"
][<|"name" -> $ScriptCommandLine[[1]], "reporters" -> possibleReporters|>];


(* When adding a new reporter, be sure to add it to this list so it's handled
   correctly *)
possibleReporters = {"spec", "tap", "json", "mathematica", "junit"};

parseTarget[argv_, prevResult_] :=
    Module[{argList, result},
      result = prevResult;
      argList = argv;
      If[Length[argList] == 0,
        CompoundExpression[
          result["exit"] = "help",
          result["msg"] = "Must specify a target.",
          result
        ],
        CompoundExpression[
          result["target"] = First[argList],
          result
        ]
      ]
    ]

parseReporter[argv_, prevResult_] :=
    Module[{argList, result},
      result = prevResult;
      argList = argv;
      If[Length[argList] == 0,
        CompoundExpression[
          result["exit"] = "help",
          result["msg"] = "Must specify a reporter.",
          result
        ],
        If[FreeQ[possibleReporters, First[argv]],
          CompoundExpression[
            result["exit"] = "help",
            result["msg"] = StringTemplate["'``' is not a valid reporter. Please choose from ``"][First[argv], possibleReporters],
            result
          ],
          CompoundExpression[
            result["reporter"] = First[argv],
            parseTarget[Rest[argList], result]
          ]
        ]
      ]
    ]

parseInitialState[argv_] :=
    Module[{},
      If[StringMatchQ[First[argv], "-*"],
        If[MatchQ[First[argv], "-h" | "--help"],
          <|"exit" -> "help"|>,
          If[MatchQ[First[argv], "-V" | "--version"],
            <|"exit" -> "version"|>,
            If[MatchQ[First[argv], "-R" | "--reporter"],
              parseReporter[Rest[argv], <||>],
              <|"exit" -> "help",
                "msg" ->
                    StringTemplate["Invalid option '``'"][First[argv]]|>
            ]
          ]
        ],
        parseTarget[argv, <|"reporter" -> "spec"|>]
      ]
    ]

parseOpts[scriptCommandLine_] :=
    Module[{argList},
      argList = Rest[scriptCommandLine];
      If[Length[argList] == 0,
        <|"exit" -> "help",
          "msg" -> "Must specify a target file or directory"|>,
        parseInitialState[argList]
      ]
    ]

argsResult = parseOpts[$ScriptCommandLine];

getTestObject[testReportObject_, outcomePattern_] :=
    Module[{topTitle, results, tests},
      topTitle = testReportObject["Title"];
      results = List @@ testReportObject["TestResults"];
      If[StringMatchQ[#["Outcome"], outcomePattern],
        <|"filetitle" -> topTitle,
          "title" -> ToString[#["TestID"]],
          "fullTitle" -> topTitle <> " - " <> ToString[#["TestID"]],
          "duration" -> N[QuantityMagnitude[#["AbsoluteTimeUsed"], "Milliseconds"]],
          "status" -> #["Outcome"],
          "err" ->
              If[#["Outcome"] == "Failure",
                <|"expected" -> StringTemplate["expected `` to equal ``"][ToString[#["ExpectedOutput"]], ToString[#["ActualOutput"]]],
                  "expectedValue" -> ToString[#["ExpectedOutput"]],
                  "actualValue" -> ToString[#["ActualOutput"]],
                  "expectedMessages" -> StringTemplate["expected `` to equal ``"][ToString[#["ExpectedMessages"]], ToString[#["ActualMessages"]]]|>,
                <||>
              ]
        |>, {}] & /@ results
    ];

getTotalDurationInMS[testResults_] :=
    IntegerPart[QuantityMagnitude[Plus @@ (#["TimeElapsed"] & /@ testResults), "Milliseconds"]]

outputJSON[testResults_] :=
    Module[{outputResults},
      outputResults = <|
        "stats" -> <|
          "files" -> Length[testResults],
          "duration" -> getTotalDurationInMS[testResults],
          "tests" -> Plus @@ Flatten[{#["TestsSucceededCount"], #["TestsFailedCount"]} & /@ testResults],
          "passes" -> Plus @@ (#["TestsSucceededCount"] & /@ testResults),
          "failures" -> Plus @@ (#["TestsFailedCount"] & /@ testResults)|>|>;
      outputResults["tests"] = Flatten[getTestObject[#, "Success" | "Failure"] & /@ testResults];
      outputResults["passes"] = Flatten[getTestObject[#, "Success"] & /@ testResults];
      outputResults["failures"] = Flatten[getTestObject[#, "Failure"] & /@ testResults];
      Print[ExportString[outputResults, "RawJSON"]];
    ];

outputMathematica[testResults_] :=
    Print[ToString[InputForm[testResults]]];

outputSpec[testResults_] :=
    Module[{successCount, failureCount, duration, esc, tests, failures},

    (*
     * Colors for printing to the console
     * See: https://mathematica.stackexchange.com/a/164487/90
     *)
      esc = Association["reset" -> "\033[1;0m", "red" -> "\033[0;31m",
        "green" -> "\033[1;32m", "gray" -> "\033[0;37m"];

      duration = getTotalDurationInMS[testResults];
      successCount = Plus @@ (#["TestsSucceededCount"] & /@ testResults);
      failureCount = Plus @@ (#["TestsFailedCount"] & /@ testResults);

      CompoundExpression[
        Print["\n  ", First[#]["filetitle"]],
        Map[Function[test,
          Switch[
            test["status"],
            "Success", Print["    ", esc["green"], "\[Checkmark] ", esc["gray"], test["title"], esc["reset"]],
            "Failure", Print["    ", esc["red"], "\[Times] ", test["title"], esc["reset"]]]], #]
      ] & /@ (getTestObject[#, "Failure" | "Success"] & /@ testResults);

      Print["\n"];
      Print["  ", esc["green"], successCount, " passing", esc["reset"], esc["gray"], StringTemplate[" (``ms)"][duration]];
      If[failureCount > 0,
        Print["  ", esc["red"], failureCount, " failing", esc["reset"], "\n"],
        Print["\n"]
      ];
      CompoundExpression[
        Print["    ", #["filetitle"]],
        Print["      ", #["title"]],
        Print["      ", esc["red"], "Verification Error: ", #["err"]["expected"], esc["reset"]];
        Print["      ", esc["green"], "+ expected ", esc["red"], "- actual\n"];
        Print["      - ", #["err"]["actualValue"], esc["green"]];
        Print["      - ", #["err"]["expectedValue"], esc["reset"], "\n"];
      ] & /@ Flatten[getTestObject[#, "Failure"] & /@ testResults];
    ];

outputTAP[testResults_] :=
    Module[{successCount, failureCount, tests, totalCount},
      successCount = Plus @@ (#["TestsSucceededCount"] & /@ testResults);
      failureCount = Plus @@ (#["TestsFailedCount"] & /@ testResults);
      totalCount = successCount + failureCount;

      tests = Flatten[getTestObject[#, "Failure" | "Success"] & /@ testResults];

      Print["1..", totalCount];

      MapIndexed[
        Print[
          If[#1["status"] == "Success", "ok ", "not ok "],
          First[#2], " ",
          #1["filetitle"], " ", #1["title"],
          If[#1["status"] == "Failure",
            "\n# Verification Error: " <> #["err"]["expected"],
            ""
          ]
        ] &,
        tests
      ];

      Print["# tests ", totalCount];
      Print["# pass ", successCount];
      Print["# fail ", failureCount];
      Print["# skip ", 0];

    ];

outputJUnit[testResults_] :=
    Module[{successCount, failureCount, tests, totalCount, duration,
      testSuites, xml, xmlString},
      successCount = Plus @@ (#["TestsSucceededCount"] & /@ testResults);
      failureCount = Plus @@ (#["TestsFailedCount"] & /@ testResults);
      totalCount = successCount + failureCount;
      duration = QuantityMagnitude[Plus @@ (#["TimeElapsed"] & /@ testResults), "Seconds"];

      testSuites = Map[
        Function[{testReportAssoc},
          XMLElement[
            "testsuite",
            {
              "name" -> testReportAssoc["Title"],
              "tests" -> ToString[testReportAssoc["TestsSucceededCount"] + testReportAssoc["TestsFailedCount"]],
              "failures" -> ToString[testReportAssoc["TestsFailedCount"]],
              "time" -> ToString[QuantityMagnitude[testReportAssoc["TimeElapsed"], "Seconds"]]
            },
            Map[Function[{testResult},
              XMLElement["testcase",
                {
                  "name" -> ToString[testResult["TestID"]],
                  "time" -> ToString[QuantityMagnitude[testResult["AbsoluteTimeUsed"]]]
                },
                If[
                  StringMatchQ[testResult["Outcome"], "Failure"],
                  {XMLElement["failure", {}, {
                    XMLObject["CDATASection"][
                      StringTemplate[
                        "Verification Error: expected `` to equal ``"][ToString[testResult["ExpectedOutput"]], ToString[testResult["ActualOutput"]]]]}]},
                  {}
                ]]],
              Values[testReportAssoc["TestResults"]]
            ]
          ]
        ], testResults];

      xml =
          XMLObject["Document"][{XMLObject["Declaration"]["Version" -> "1.0", "Encoding" -> "UTF-8"]},
            XMLElement["testsuites",
              {
                "name" -> "Mathematica Tests",
                "time" -> ToString[QuantityMagnitude[Plus @@ (#["TimeElapsed"] & /@ testResults), "Seconds"]],
                "tests" -> ToString[totalCount],
                "failures" -> ToString[failureCount]
              },
              Prepend[testSuites, XMLElement["testsuite", {"name" -> "Root Suite", "tests" -> "0", "failures" -> "0", "time" -> "0"}, {}]]],
            {}
          ];

      Print[ExportString[xml, "XML"]]
    ]

If[KeyExistsQ[argsResult, "exit"],
  Switch[argsResult["exit"],
    "version", Print[versionInfo[]]; Exit[];,
    "help", If[KeyExistsQ[argsResult, "msg"], Print[argsResult["msg"]]]; Print[usage[]]; Exit[];
  ];
];

Module[{fileList, results},
  fileList = {};

  fileList = FileNames["*", argsResult["target"], IgnoreCase -> True];
  If[Length[fileList] == 0,
    If[FileExistsQ[argsResult["target"]],
      AppendTo[fileList, argsResult["target"]]
    ]
  ];

  If[Length[fileList] == 0,
    Print[StringTemplate["The target '``' is neither a directory with test files, nor an individual test file."][argsResult["target"]]]; Exit[]
  ];

  results = TestReport /@ fileList;

  Switch[argsResult["reporter"],
    "mathematica", outputMathematica[results],
    "json", outputJSON[results],
    "tap", outputTAP[results],
    "spec", outputSpec[results],
    "junit", outputJUnit[results]
  ];
]



